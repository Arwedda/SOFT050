<html>
    <head>
        <title>Hold'Em</title>
        <meta http-equiv="x-ua-compatible" content="IE=10" /> 
    </head>
    <body background ="felt.png">
        <input id='btnStart' type='button' value='Start' />
        <input id='btnFold' type='button' value='Fold' disabled='disabled' onclick='iPChoice = 6; Main()' />
        <input id='btnBet0' type='button' value='Check/Call' disabled='disabled' onclick='iPChoice = 0; Main()' /><br />
        <input id='btnBet1' type='button' value='Bet Blind' disabled='disabled' onclick='iPChoice = 1; Main()' />
        <input id='btnBet2' type='button' value='Bet 2x Blind' disabled='disabled' onclick='iPChoice = 2; Main()' />
        <input id='btnBet3' type='button' value='Bet 3x Blind' disabled='disabled' onclick='iPChoice = 3; Main()' /><br />
        <input id='btnBet4' type='button' value='Bet 4x Blind' disabled='disabled' onclick='iPChoice = 4; Main()' />
        <input id='btnBet5' type='button' value='All In' disabled='disabled' onclick='iPChoice = 5; Main()' />
        <img id='imgCC0' src='bk.jpg' height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <img id='imgCC1' src='bk.jpg' height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <img id='imgCC2' src='bk.jpg' height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <img id='imgCC3' src='bk.jpg' height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <img id='imgCC4' src='bk.jpg' height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <p id='parPChips' style='position: absolute; background-color: lightsteelblue; border-color: brown; border-style: ridge; border-width: 2px; border-radius: 10px;'></p>
        <img id='imgPC0' src="bk.jpg" height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <img id='imgPC1' src="bk.jpg" height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <p id='parC1Chips' style='position: absolute; background-color: lightsteelblue; border-color: brown; border-style: ridge; border-width: 2px; border-radius: 10px;'></p>
        <img id='imgC1C0' src="bk.jpg" height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <img id='imgC1C1' src="bk.jpg" height='250' width='200' style='position: absolute; border-radius: 10px;' />
        <p id='parResult' style='position: absolute; background-color: lightsteelblue; border-color: brown; border-style: ridge; border-width: 2px; border-radius: 10px;'></p>
    </body>
</html>
<script language="vbscript" src="PokerHands.vbs"></script>
<script language="vbscript" src="Betting.vbs"></script>
<script language="vbscript">
Option Explicit
Dim sDeck   'Game/Communal Variables & Arrays
Dim sSixCardCombos
Dim sSevCardCombos
Dim iPair(1)
Dim sHighcard
Dim iThreeOfAKind
Dim bStraight
Dim bFlush
Dim iRound
Dim iHandNum
Dim sHandRes
Dim iCheckVals(4)
Dim sCheckSuits(4)
Dim iCheckScore
Dim sCheckHandDesc
Dim sCCard(4)
Dim sCSuit(4)
Dim iCValue(4)
Dim iBlind
Dim iPot
Dim iAnte
Dim iPBestScore 'Player Variables & Arrays
Dim sPBestHandDesc
Dim sPCard(1)
Dim iPValue(6)
Dim sPSuit(6)
Dim iPChips
Dim iPRoundBet
Dim iPChoice
Dim iC1BestScore    'Computer 1 Variables & Arrays
Dim sC1BestHandDesc
Dim sC1Card(1)
Dim iC1Value(6)
Dim sC1Suit(6)
Dim iC1Chips
Dim iC1RoundBet
Dim bC1Fold
Dim bC1Moved

    Sub window_Onload()
        Randomize
        Initialise
    End Sub

    Sub Initialise()
        CreateCombinations
        Init 15000
        Interface
    End Sub

    Sub btnStart_onClick()
        PrepareHand
    End Sub

    Sub PrepareHand()
        FreshDeck
        FreshHand
        StartEndLock "", "Disabled"
        DealHCs
        Blinds
        PlayerHCards
        ComputerHCards
        ShowBet
        iRound = (iRound+1)
    End Sub

    Sub PlayerHCards()
        SuitsAndValues iPValue, sPSuit, "imgPC", sPCard, 0, 1
        Sort iPValue, sPSuit, sPCard, 0, True
        SuitsAndValues iPValue, sPSuit, "imgPC", sPCard, 0, 1
        CheckHand iPValue, sPSuit, 1, iPBestScore, sPBestHandDesc
        ShowCards "imgPC", sPCard, 0, 1
    End Sub

    Sub ComputerHCards()
        SuitsAndValues iC1Value, sC1Suit, "imgC1C", sC1Card, 0, 1
        Sort iC1Value, sC1Suit, sC1Card, 0, True
        SuitsAndValues iC1Value, sC1Suit, "imgC1C", sC1Card, 0, 1
        CheckHand iC1Value, sC1Suit, 1, iC1BestScore, sC1BestHandDesc
    End Sub

    Sub Main()
        Dim Desc
        If iPChoice = 6 Then
            Fold iC1Chips, "You fold the pot; Computer 1 wins."
        Else
            If iRound = 1 Then
                If bC1Moved = False Then
                    PlayerBet 100, 12, 11, 10, 9
                Else
                    PlayerCall
                    Flop
                End If
            ElseIf iRound = 2 Then
                If bC1Moved = False Then
                    PlayerBet 10000, 2000, 800, 200, 20
                Else
                    PlayerCall
                    Turn
                End If
            ElseIf iRound = 3 Then
                If bC1Moved = False Then
                    PlayerBet 10000, 2000, 800, 200, 20
                Else
                    PlayerCall
                    River
                End If
            ElseIf iRound = 4 Then
                If bC1Moved = False Then
                    PlayerBet 10000, 2000, 800, 200, 20
                Else
                    PlayerCall
                    HandOver
                End If
            End If
        End If
    End Sub

    Sub Flop()    
        CommFlop
        PlayerFlop
        ComputerFlop
        ShowFlop
        ShowBet
    End Sub

    Sub CommFlop()
        DealFlop
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 0, 2
        Sort iCValue, sCSuit, sCCard, 1, True
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 0, 2
    End Sub

    Sub PlayerFlop()
        ClearHandVars
        MergeCards sPSuit, iPValue, sCSuit, iCValue, 0, 2 
        Sort iPValue, sPSuit, sPCard, 3, False
        ClearHandVars
        CheckHand iPValue, sPSuit, 4, iPBestScore, sPBestHandDesc
    End Sub

    Sub ComputerFlop()
        ClearHandVars
        MergeCards sC1Suit, iC1Value, sCSuit, iCValue, 0, 2 
        Sort iC1Value, sC1Suit, sC1Card, 3, False
        ClearHandVars
        CheckHand iC1Value, sC1Suit, 4, iC1BestScore, sC1BestHandDesc
    End Sub

    Sub ShowFlop()
        If bC1Fold = False Then
            ShowCards "imgCC", sCCard, 0, 2
        End if
    End Sub

    Sub Turn()
        CommTurn
        PlayerTurn
        ComputerTurn
        ShowTurn
        ShowBet
    End Sub

    Sub CommTurn()
        DealTurn 
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 3, 3
        Sort iCValue, sCSuit, sCCard, 2, False
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 3, 3
    End Sub

    Sub PlayerTurn()
        ClearHandVars
        MergeCards sPSuit, iPValue, sCSuit, iCValue, 3, 3
        Sort iPValue, sPSuit, sPCard, 4, False
        ClearHandVars
        CompareHands sSixCardCombos, iPValue, sPSuit, 5, iPBestScore, sPBestHandDesc
    End Sub

    Sub ComputerTurn()
        ClearHandVars
        MergeCards sC1Suit, iC1Value, sCSuit, iCValue, 3, 3
        Sort iC1Value, sC1Suit, sC1Card, 4, False
        ClearHandVars
        CompareHands sSixCardCombos, iC1Value, sC1Suit, 5, iC1BestScore, sC1BestHandDesc
    End Sub

    Sub ShowTurn()
        If bC1Fold = False Then
            ShowCards "imgCC", sCCard, 3, 3
        End If
    End Sub

    Sub River()
        CommRiver
        PlayerRiver
        ComputerRiver
        ShowRiver
        ShowBet
    End Sub

    Sub CommRiver()
        DealRiver
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 4, 4
        Sort iCValue, sCSuit, sCCard, 3, False
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 4, 4
    End Sub

    Sub PlayerRiver()
        ClearHandVars
        MergeCards sPSuit, iPValue, sCSuit, iCValue, 4, 4
        Sort iPValue, sPSuit, sPCard, 5, False
        ClearHandVars
        CompareHands sSevCardCombos, iPValue, sPSuit, 20, iPBestScore, sPBestHandDesc
    End Sub

    Sub ComputerRiver()
        ClearHandVars
        MergeCards sC1Suit, iC1Value, sCSuit, iCValue, 4, 4
        Sort iC1Value, sC1Suit, sC1Card, 5, False
        ClearHandVars
        CompareHands sSevCardCombos, iC1Value, sC1Suit, 20, iC1BestScore, sC1BestHandDesc
    End Sub

    Sub ShowRiver()
        If bC1Fold = False Then
            ShowCards "imgCC", sCCard, 4, 4
        End If
    End Sub

    Sub HandOver()
        If bC1Fold = False Then
            ShowCards "imgC1C", sC1Card, 0, 1
            Winner
            StartEndLock "Disabled", ""
        End If
        If iC1Chips < 1 Then
            Alert("Congratulations! You win! Play again?")
            Initialise
        ElseIf iPChips < 1 Then
            Alert("Unlucky! You lose! Play again?")
            Initialise
        End If
    End Sub

    Sub AllInRounds()
        ShowCards "imgC1C", sC1Card, 0, 1
        If iRound = 1 Then
            Flop
            iRound = (iRound+1)
        End If
        If iRound = 2 Then
            Turn
            iRound = (iRound+1)
        End If
        River
        HandOver
    End Sub
</script>