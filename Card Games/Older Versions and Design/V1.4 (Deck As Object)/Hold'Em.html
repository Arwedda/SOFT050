<html>
    <head>
        <title>Hold'Em</title>
        <meta http-equiv="x-ua-compatible" content="IE=10" /> 
    </head>
    <body>
        <input id='btnStart' type='button' value='Start' />
        <input id='btnFold' type='button' value='Fold' disabled='disabled' onclick='iPChoice = 6; Main()' />
        <input id='btnBet0' type='button' value='Check/Call' disabled='disabled' onclick='iPChoice = 0; Main()' /><br />
        <input id='btnBet1' type='button' value='Bet Blind' disabled='disabled' onclick='iPChoice = 1; Main()' />
        <input id='btnBet2' type='button' value='Bet 2x Blind' disabled='disabled' onclick='iPChoice = 2; Main()' />
        <input id='btnBet3' type='button' value='Bet 3x Blind' disabled='disabled' onclick='iPChoice = 3; Main()' /><br />
        <input id='btnBet4' type='button' value='Bet 4x Blind' disabled='disabled' onclick='iPChoice = 4; Main()' />
        <input id='btnBet5' type='button' value='All In' disabled='disabled' onclick='iPChoice = 5; Main()' />
        <img id='imgCC0' src='bk.jpg' height='250' width='200' style='position: absolute;' />
        <img id='imgCC1' src='bk.jpg' height='250' width='200' style='position: absolute;' />
        <img id='imgCC2' src='bk.jpg' height='250' width='200' style='position: absolute;' />
        <img id='imgCC3' src='bk.jpg' height='250' width='200' style='position: absolute;' />
        <img id='imgCC4' src='bk.jpg' height='250' width='200' style='position: absolute;' />
        <p id='parPChips' style='position: absolute;'></p>
        <img id='imgPC0' src="bk.jpg" height='250' width='200' style='position: absolute;' />
        <img id='imgPC1' src="bk.jpg" height='250' width='200' style='position: absolute;' />
        <p id='parC1Chips' style='position: absolute;'></p>
        <img id='imgC1C0' src="bk.jpg" height='250' width='200' style='position: absolute;' />
        <img id='imgC1C1' src="bk.jpg" height='250' width='200' style='position: absolute;' />
        <p id='parResult' style='position: absolute;'></p>
    </body>
</html>
<script language="vbscript" src="PokerHands.vbs"></script>
<script language="vbscript" src="Betting.vbs"></script>
<script language="vbscript">
Option Explicit
Dim Cards
Set Cards = New Deck
Dim sSixCardCombos
Dim sSevCardCombos
Dim iPair(1)
Dim sHighcard
Dim iThreeOfAKind
Dim bStraight
Dim bFlush
Dim iRound
Dim iHandNum
Dim sHandRes
Dim iCheckVals(4)
Dim sCheckSuits(4)
Dim iCheckScore
Dim sCheckHandDesc
Dim sCCard(4)
Dim sCSuit(4)
Dim iCValue(4)
Dim iBlind
Dim iPot
Dim iAnte
Dim iPBestScore 'Player Variables & Arrays
Dim sPBestHandDesc
Dim sPCard(1)
Dim sPSuit(6)
Dim iPValue(6)
Dim iPChips
Dim iPRoundBet
Dim iPChoice
Dim iC1BestScore 'Computer 1 Variables & Arrays
Dim sC1BestHandDesc
Dim sC1Card(1)
Dim sC1Suit(6)
Dim iC1Value(6)
Dim iC1Chips
Dim iC1RoundBet
Dim bC1Fold
Dim bC1Moved

    Class Deck
        Dim sPicture
        Dim iValue(51)
        Dim sSuit(51)
        
        Sub InitDeck()
            Dim i
            FreshDeck
            For i = 0 to 51
                sSuit(i) = Left(sPicture(i), 1)
                iValue(i) = Int(Mid(sPicture(i), 2,2))
            Next
        End Sub

        Sub FreshDeck()
            sPicture = Array("C14.png", "C 2.png", "C 3.png", "C 4.png", "C 5.png", "C 6.png", "C 7.png","C 8.png", "C 9.png", "C10.png", "C11.png", "C12.png", "C13.png", _
            "D14.png", "D 2.png", "D 3.png", "D 4.png", "D 5.png", "D 6.png", "D 7.png", "D 8.png", "D 9.png", "D10.png", "D11.png", "D12.png", "D13.png", _
            "H14.png", "H 2.png", "H 3.png", "H 4.png", "H 5.png", "H 6.png", "H 7.png", "H 8.png", "H 9.png", "H10.png", "H11.png", "H12.png", "H13.png", _
            "S14.png", "S 2.png", "S 3.png", "S 4.png", "S 5.png", "S 6.png", "S 7.png", "S 8.png", "S 9.png", "S10.png", "S11.png", "S12.png", "S13.png")
        End Sub

        Sub RemoveCard(Card, k, l)
            Dim i
            Dim j
            For i = k to l
                For j = 0 to 51
                    If Card(i) = sPicture(j) Then
                        sPicture(j) = ""
                    End If
                Next
            Next
        End Sub
    End Class

    Sub window_Onload()
        Randomize
        Initialise
    End Sub

    Sub Initialise()
        CreateCombinations
        Init 15000
        Interface
    End Sub

    Sub btnStart_onClick()
        PrepareHand
    End Sub

    Sub PrepareHand()
        StartEndLock "", "Disabled"
        Cards.FreshDeck
        FreshHand
        DealHCs
        Blinds
        PlayerHCards
        ComputerHCards
    End Sub

    Sub PlayerHCards()
        Sort iPValue, sPSuit, sPCard, 0, True
        CheckHand iPValue, sPSuit, 1, iPBestScore, sPBestHandDesc
        ShowCards "imgPC", sPCard, 0, 1
    End Sub

    Sub ComputerHCards()
        Sort iC1Value, sC1Suit, sC1Card, 0, True
        CheckHand iC1Value, sC1Suit, 1, iC1BestScore, sC1BestHandDesc
        ShowCards "imgC1C", sC1Card, 0, 1
    End Sub

    Sub Main()
        Dim Desc
        If iPChoice = 6 Then
            Fold iC1Chips, "You fold the pot; Computer 1 wins."
        Else
            If iRound = 0 Then
                If bC1Moved = False Then
                    PlayerBet 100, 12, 11, 10, 9
                Else
                    PlayerCall
                    Flop
                End If
            ElseIf iRound = 1 Then
                If bC1Moved = False Then
                    PlayerBet 10000, 2000, 800, 200, 20
                Else
                    PlayerCall
                    Turn
                End If
            ElseIf iRound = 2 Then
                If bC1Moved = False Then
                    PlayerBet 10000, 2000, 800, 200, 20
                Else
                    PlayerCall
                    River
                End If
            ElseIf iRound = 3 Then
                If bC1Moved = False Then
                    PlayerBet 10000, 2000, 800, 200, 20
                Else
                    PlayerCall
                    HandOver
                End If
            End If
        End If
    End Sub

    Sub Flop()    
        CommFlop
        PlayerFlop
        ComputerFlop
        ShowFlop
    End Sub

    Sub CommFlop()
        DealFlop
        Sort iCValue, sCSuit, sCCard, 1, True
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 0, 2
    End Sub

    Sub PlayerFlop()
        ClearHandVars
        MergeCards sPSuit, iPValue, sCSuit, iCValue, 0, 2 
        Sort iPValue, sPSuit, sPCard, 3, False
        ClearHandVars
        CheckHand iPValue, sPSuit, 4, iPBestScore, sPBestHandDesc
    End Sub

    Sub ComputerFlop()
        ClearHandVars
        MergeCards sC1Suit, iC1Value, sCSuit, iCValue, 0, 2 
        Sort iC1Value, sC1Suit, sC1Card, 3, False
        ClearHandVars
        CheckHand iC1Value, sC1Suit, 4, iC1BestScore, sC1BestHandDesc
    End Sub

    Sub ShowFlop()
        If bC1Fold = False Then
            ShowCards "imgCC", sCCard, 0, 2
        End if
    End Sub

    Sub Turn()
        CommTurn
        PlayerTurn
        ComputerTurn
        ShowTurn
    End Sub

    Sub CommTurn()
        DealTurn 
        Sort iCValue, sCSuit, sCCard, 2, False
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 3, 3
    End Sub

    Sub PlayerTurn()
        ClearHandVars
        MergeCards sPSuit, iPValue, sCSuit, iCValue, 3, 3
        Sort iPValue, sPSuit, sPCard, 4, False
        ClearHandVars
        CompareHands sSixCardCombos, iPValue, sPSuit, 5, iPBestScore, sPBestHandDesc
    End Sub

    Sub ComputerTurn()
        ClearHandVars
        MergeCards sC1Suit, iC1Value, sCSuit, iCValue, 3, 3
        Sort iC1Value, sC1Suit, sC1Card, 4, False
        ClearHandVars
        CompareHands sSixCardCombos, iC1Value, sC1Suit, 5, iC1BestScore, sC1BestHandDesc
    End Sub

    Sub ShowTurn()
        If bC1Fold = False Then
            ShowCards "imgCC", sCCard, 3, 3
        End If
    End Sub

    Sub River()
        CommRiver
        PlayerRiver
        ComputerRiver
        ShowRiver
    End Sub

    Sub CommRiver()
        DealRiver
        Sort iCValue, sCSuit, sCCard, 3, False
        SuitsAndValues iCValue, sCSuit, "imgCC", sCCard, 4, 4
    End Sub

    Sub PlayerRiver()
        ClearHandVars
        MergeCards sPSuit, iPValue, sCSuit, iCValue, 4, 4
        Sort iPValue, sPSuit, sPCard, 5, False
        ClearHandVars
        CompareHands sSevCardCombos, iPValue, sPSuit, 20, iPBestScore, sPBestHandDesc
    End Sub

    Sub ComputerRiver()
        ClearHandVars
        MergeCards sC1Suit, iC1Value, sCSuit, iCValue, 4, 4
        Sort iC1Value, sC1Suit, sC1Card, 5, False
        ClearHandVars
        CompareHands sSevCardCombos, iC1Value, sC1Suit, 20, iC1BestScore, sC1BestHandDesc
    End Sub

    Sub ShowRiver()
        If bC1Fold = False Then
            ShowCards "imgCC", sCCard, 4, 4
        End If
    End Sub
            'Continue from here
    Sub HandOver()
        If bC1Fold = False Then
            Winner
            StartEndLock "Disabled", ""
        End If
        If iC1Chips < 1 Then
            Alert("Congratulations! You win! Play again?")
            Initialise
        ElseIf iPChips < 1 Then
            Alert("Unlucky! You lose! Play again?")
            Initialise
        End If
    End Sub
</script>